// STD IO

function printc(c)
{
	__movePtr(c);
	__bf(".");
}

function printd(x)
{
	prints(to_string(x));
}

function prints(x)
{
	i = 0;
	while (i < sizeof(x) && x[i] != 0)
		printc(x[i++]);
}


function println(x)
{
	prints(x);
	printc('\n');
}


function endl()
{
	printc('\n');
}

function c = scanc()
{
	/* The argument to __movePtr is not evaluated,
	so the return value has to be instantiated explicitly. */

	c; __movePtr(c);
	__bf(",");
}

function result = scand()
{
	[3]input = 0;
	input = scans(input);

	[]powers = #(1, 10, 100);
	result = 0;
	j = 0;
	for (i = 0; i != 3; ++i)
	{
		x = input[2 - i];
		if (x != 0)
			result += (x - '0') * powers[j++];
	}
}

function buf = scans(buf)
{
	i = 0;
	done = 0;
	while (i < sizeof(buf) && !done)
	{
		c = scanc();
		if (c != '\n')
		{
			buf[i] = c;
			++i;
		}
		else
		{
			buf[i] = 0;
			done = 1;
		}
	}
}

function str = to_string(x)
{
	[3]str = 0;

	hundreds = x / 100;
	x -= 100 * hundreds;
	
	tens = x / 10;
	x -= 10 * tens;

	ones = x;
	idx = 0;
	
	if (hundreds != 0)
	{
		str[idx++] = hundreds + '0';
		str[idx++] = tens + '0';
	}
	else if (tens != 0)
	{
		str[idx++] = tens + '0';
	}

	str[idx] = ones + '0';
}

function x = to_int(str)
{
	
}
