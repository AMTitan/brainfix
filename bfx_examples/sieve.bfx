include "std/io.bfx"
include "std/math.bfx"

const MAX_INT_SIZE = 255;
const ARRAY_SIZE = 250;

function x = incr(x, p, n)
{
    if (MAX_INT_SIZE - x < p) // would cause overflow
        x = n;
    else
        x += p;    // safe to increment
}

function main()
{
    println("Welcome to the BrainF*ck prime-sieve!");
    prints("Enter n (0-250): ");
    n = scand();

    // Initialize array and calculate maximum sieve-value
    [ARRAY_SIZE]arr = 1;
    s = sqrt(n);
    
    // Sieve
    prime = 2;
    while (prime <= s)
    {
        for (i = prime * prime; i < n; i = incr(i, prime, n)) // protect against overflow
            arr[i] = 0;

        ++prime;
        while (arr[prime] == 0)
            ++prime;
    }

    // Print results
    count = 0;
    for (i = 2; i < n; ++i)
    {
        if (arr[i])
        {
            printd(i); endl();
            ++count;
        }
    }

    prints("Found "); printd(count); println(" primes!");
}
