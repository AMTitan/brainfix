 %class-name Scanner
%filenames scanner
%lex-source = "lex_flexcpp_generated.cc"

ident [_a-zA-Z][_a-zA-Z0-9]*
blanks [[:blank:]\n]

%x string
%x character
%x eolcomment
%x ccomment

%%

{blanks}                    // ignore

"include"                   return CompilerBase::INCLUDE;
"function"                  return CompilerBase::FUNCTION;
"let"                       return CompilerBase::LET;
"global"                    return CompilerBase::GLOBAL;
"const"                     return CompilerBase::CONST;
"for"                       return CompilerBase::FOR;
"if"                        return CompilerBase::IF;
"else"                      return CompilerBase::ELSE;
"while"                     return CompilerBase::WHILE;
"++"                        return CompilerBase::INC;
"--"                        return CompilerBase::DEC;
"+="                        return CompilerBase::ADD;
"-="                        return CompilerBase::SUB;
"*="                        return CompilerBase::MUL;
"/="                        return CompilerBase::DIV;
"%="                        return CompilerBase::MOD;
"/=%"                       return CompilerBase::DIVMOD;
"%=/"                       return CompilerBase::MODDIV;
"<="                        return CompilerBase::LE;
">="                        return CompilerBase::GE;
"=="                        return CompilerBase::EQ;
"!="                        return CompilerBase::NE;
"&&"                        return CompilerBase::AND;
"||"                        return CompilerBase::OR;
"__bf"                      return CompilerBase::INLINE_BF;
"__movePtr"                 return CompilerBase::MOVE_PTR;
"sizeof"                    return CompilerBase::SIZEOF;


[[:digit:]]+                return CompilerBase::NUM;
{ident}                     return CompilerBase::IDENT;

"'"."'"                     {
                                setMatched(matched().substr(1, 1));
                                return CompilerBase::CHR;
                            }
"'\\"."'"                   {
                                setMatched(escape(matched()[2]));
                                return CompilerBase::CHR;
                            }

\"                          {
                                more();
                                begin(StartCondition_::string);
                            }

"//"                        {
                                more();
                                begin(StartCondition_::eolcomment);
                            }

"/*"                        {
                                more();
                                begin(StartCondition_::ccomment);
                            }

.|\n|\r                     return matched()[0];
                            
<string>{
\"                          {
                                begin(StartCondition_::INITIAL);
                                setMatched(matched().substr(1, matched().size() - 2));
                                return CompilerBase::STR;
                            }

"\\".                       {
                                setMatched(escape(matched()));
                                more();
                            }
                            
.                           more();
}

<eolcomment>{
\n|\r                       begin(StartCondition_::INITIAL);
.                           more();
}
                
<ccomment>{
"*/"                        begin(StartCondition_::INITIAL);
.|\n|\r                     more();
}
